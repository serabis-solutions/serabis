#!/usr/bin/env perl
use strict;
use warnings;
use FindBin qw/$Bin/;
use lib "$Bin/lib";
use JSON::Tiny qw/to_json/;

open my $meminfo_fh, "<", "/proc/meminfo";
my %meminfo;

while ( <$meminfo_fh> ) {
    chomp;

    #change camel to snake case
    $_ =~ s/((?<=[a-z])[A-Z]+)/_\U$1/g;
    my ($key, $value) = $_ =~ /^(.+):\s+(\d+)/;

    $meminfo{ lc $key } = $value;
}

if ( -e "/sys/kernel/mm/ksm/run" ) {
    open my $ksm_run_fh, "<", "/sys/kernel/mm/ksm/run";
    my $ksm_run = <$ksm_run_fh>;
    chomp $ksm_run;

    if ( $ksm_run ) {
        open my $pages_sharing_fh, "<", "/sys/kernel/mm/ksm/pages_sharing";
        open my $pages_shared_fh, "<", "/sys/kernel/mm/ksm/pages_shared";
        my $pages_sharing = <$pages_sharing_fh>;
        my $pages_shared = <$pages_shared_fh>;

        chomp $pages_sharing;
        chomp $pages_shared;

        my $ksm_saved = $pages_sharing - $pages_shared;

        $meminfo{ksm_saved} = $ksm_saved;
    }
}

print to_json( \%meminfo );
